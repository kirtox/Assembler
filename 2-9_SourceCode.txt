 5	COPY	START	0		COPY FILE FROM INPUT TO OUTPUT
10	FIRST	STL	RETADR		SAVE RETURN ADDRESS
13		LDB    #LENGTH		ESTABLEISH BASE REGISTER
14		BASE	LENGTH
15	CLOOP  +JSUB	RDREC		READ INPUT RECORD
20		LDA	LENGTH		TEST FOR EOF (LENGTH = 0)
25		COMP   #0
30		JEQ	ENDFIL		EXIT IF EOF FOUND
35	       +JSUB	WRREC		WRITE OUTPUT RECORD
40		J	CLOOP		LOOP
45	ENDFIL	LDA    =C'EOF'		INSERT END OF FILE MARKER
50		STA	BUFFER
55		LDA    #3		SET LENGTH = 3
60		STA	LENGTH
65	       +JSUB	WRREC		WRITE EOF
70		J      @RETADR		RETURN TO CALLER
93		LTORG	
95	RETADR	RESW	1
100	LENGTH	RESW	1		LENGTH OF RECORD
105	BUFFER	RESB	4096		4096-BYTE BUFFER AREA
106	BUFEND	EQU	*
107	MAXLEN	EQU	BUFEND-BUFFER	MAXIMUM RECORD LENGTH 	
110	.
115	.	SUBROUTINE TO READ RECORD INTO BUFFER
120	.
125	RDREC	CLEAR	X		CLEAR LOOP COUNTER
130		CLEAR	A		CLEAR A TO ZERO
132		CLEAR	S		CLEAR S TO ZERO
133	       +LDT    #MAXLEN
135	RLOOP	TD	INPUT		TEST INPUT DEVICE
140		JEQ	RLOOP		LOOP UNTIL READY
145		RD	INPUT		READ CHARACTER INTO REGISTER A
150		COMPR	A,S		TEST FOR END OF RECORD (X'00')
155		JEQ	EXIT		EXIT LOOP IF EOF
160		STCH	BUFFER,X	STORE CHARACTER IN BUFFER
165		TIXR	T		LOOP UNLESS MAX LENGTH
170		JLT	RLOOP		  HAS BEEN REACHED
175	EXIT	STX	LENGTH		SAVE RECORD LENGTH
180		RSUB			RETURN TO CALLER
185	INPUT	BYTE	X'F1'		CODE FOR INPUT DEVICE
195	.
200	.	SUBROUTINE TO WRITE RECORD FROM BUFFER
205	.
210	WRREC	CLEAR	X		CLEAR LOOP COUNTER
212		LDT	LENGTH
215	WLOOP	TD     =X'05'		TEST OUTPUT DEVICE
220		JEQ	WLOOP		LOOP UNTIL READY
225		LDCH	BUFFER,X	GET CHARACTER FROM BUFFER
230		WD     =X'05'		WRITE CHARACTER
235		TIXR	T		LOOP UNTIL ALL CHARACTERS
240		JLT	WLOOP		  HAVE BEEN WRITTEN
245		RSUB			RETURN TO CALLER
255		END	FIRST